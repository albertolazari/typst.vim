" Vim syntax file
" Language: Typst
" Maintainer: Kaj Munhoz Arfvidsson
" Latest Revision: Apr 2023

if exists("b:current_syntax")
  finish
endif

" Common {{{1
syntax cluster typstCommon
    \ contains=@typstComment

" Common > Comment {{{2
syntax cluster typstComment
    \ contains=typstCommentBlock,typstCommentLine
syntax match typstCommentBlock
    \ #/\*\_.*\*/#
    \ contains=typstCommentTodo,@Spell
syntax match typstCommentLine
    \ #//.*$#
    \ contains=typstCommentTodo,@Spell
syntax keyword typstCommentTodo
    \ contained
    \ TODO FIXME XXX TBD


" Code {{{1
syntax cluster typstCode
    \ contains=@typstCommon
            \ ,@typstCodeKeywords
            \ ,@typstCodeConstants
            \ ,@typstCodeIdentifiers
            \ ,@typstCodeFunctions
            \ ,@typstCodeParens

" Code > Keywords {{{2
syntax cluster typstCodeKeywords
    \ contains=typstCodeConditional
            \ ,typstCodeRepeat
            \ ,typstCodeKeyword
            \ ,typstCodeStatement
syntax keyword typstCodeConditional
    \ contained
    \ if else
syntax keyword typstCodeRepeat
    \ contained
    \ while for
syntax keyword typstCodeKeyword
    \ contained
    \ not in and or return
syntax region typstCodeStatement
    \ contained
    \ matchgroup=typstCodeStatementWord start=/\v(let|set|show|import|include)>-@!/ end=/\v%(;|$)/
    \ contains=@typstCode

" Code > Constants {{{2
syntax cluster typstCodeConstants
    \ contains=typstCodeConstant
            \ ,typstCodeNumberInteger
            \ ,typstCodeNumberFloat
            \ ,typstCodeNumberLength
            \ ,typstCodeNumberAngle
            \ ,typstCodeNumberRatio
            \ ,typstCodeNumberFraction
            \ ,typstCodeString
syntax keyword typstCodeConstant
    \ contained
    \ none auto true false
syntax match typstCodeNumberInteger
    \ contained
    \ /\v<\d+>/
syntax match typstCodeNumberFloat
    \ contained
    \ /\v<\d+\.\d*>/
syntax match typstCodeNumberLength
    \ contained
    \ /\v<\d+(\.\d*)?(pt|mm|cm|in|em)>/
syntax match typstCodeNumberAngle
    \ contained
    \ /\v<\d+(\.\d*)?(deg|rad)>/
syntax match typstCodeNumberRatio
    \ contained
    \ /\v<\d+(\.\d*)?\%>/
syntax match typstCodeNumberFraction
    \ contained
    \ /\v<\d+(\.\d*)?fr>/
syntax region typstCodeString
    \ contained
    \ start=/"/ skip=/\v\\\\|\\"/ end=/"/
    \ contains=@Spell

" Code > Identifiers {{{2
syntax cluster typstCodeIdentifiers
    \ contains=typstCodeIdentifier
            \ ,typstCodeFieldAccess
syntax match typstCodeIdentifier
    \ contained
    \ /\v\K\k*%(-+\k+)*>-@!(<%(let|set|show|import|include))@<![\.\[\(]@!/
syntax match typstCodeFieldAccess
    \ contained
    \ /\v\K\k*%(-+\k+)*>-@!(<%(let|set|show|import|include))@<!\.[\[\(]@!/
    \ nextgroup=typstCodeFieldAccess,typstCodeFunction

" Code > Functions {{{2
syntax cluster typstCodeFunctions
    \ contains=typstCodeFunction
syntax match typstCodeFunction
    \ contained
    \ /\v\K\k*%(-+\k+)*[\(\[]@=/
    \ nextgroup=typstCodeFunctionArgument
syntax match typstCodeFunctionArgument
    \ contained
    \ /\v%(.{-}%(\(.{-}\)|\[.{-}\]|\{.{-}\}))*/ transparent
    \ contains=@typstCode

" Code > Parens {{{2
syntax cluster typstCodeParens
    \ contains=typstCodeParen
            \ ,typstCodeBrace
            \ ,typstCodeBracket
            \ ,typstCodeDollar
syntax region typstCodeParen
    \ contained
    \ matchgroup=Noise start=/\v\(/ end=/\v\)/
    \ contains=@typstCode
syntax region typstCodeBrace
    \ contained
    \ matchgroup=Noise start=/\v\{/ end=/\v\}/
    \ contains=@typstCode
syntax region typstCodeBracket
    \ contained
    \ matchgroup=Noise start=/\v\[/ end=/\v\]/
    \ contains=@typstMarkup
syntax region typstCodeDollar
    \ contained
    \ matchgroup=Number start=/\v\$/ end=/\v\$/
    \ contains=@typstMath


" Hashtag {{{1
syntax cluster typstHashtag
    \ contains=typstHashtagKeywords
            \ ,typstHashtagConstants
            \ ,typstHashtagIdentifiers
            \ ,typstHashtagFunctions
            \ ,typstHashtagParens

" Hashtag > Keywords {{{2
syntax cluster typstHashtagKeywords
    \ contains=typstHashtagConditional
            \ ,typstHashtagRepeat
            \ ,typstHashtagKeywords
            \ ,typstHashtagStatement
syntax match typstHashtagConditional
    \ /\v#if>-@!/
    \ skipwhite nextgroup=@typstCode
syntax match typstHashtagRepeat
    \ /\v#(while|for)>-@!/
    \ skipwhite nextgroup=@typstCode
syntax match typstHashtagKeyword
    \ /\v#(return)>-@!/
    \ skipwhite nextgroup=@typstCode
syntax region typstHashtagStatement
    \ matchgroup=typstHashtagStatementWord start=/\v#(let|set|show|import|include)>-@!/ end=/\v%(;|$)/
    \ contains=@typstCode

" Hashtag > Constants {{{2
syntax cluster typstHashtagConstants
    \ contains=typstHashtagConstant
syntax match typstHashtagConstant
    \ /\v#(none|auto|true|false)>-@!/

" Hashtag > Identifiers {{{2
syntax cluster typstHashtagIdentifiers
    \ contains=typstHashtagIdentifier
            \ ,typstHashtagFieldAccess
syntax match typstHashtagIdentifier
    \ /\v#\K\k*%(-+\k+)*>-@!(<%(let|set|show|import|include))@<![\.\[\(]@!/
syntax match typstHashtagFieldAccess
    \ /\v#\K\k*%(-+\k+)*>-@!(<%(let|set|show|import|include))@<!\.[\[\(]@!/
    \ nextgroup=typstCodeFieldAccess,typstCodeFunction

" Hashtag > Functions {{{2
syntax cluster typstHashtagFunctions
    \ contains=typstHashtagFunction
syntax match typstHashtagFunction
    \ /\v#\K\k*%(-+\k+)*[\(\[]@=/
    \ nextgroup=typstCodeFunctionArgument

" Hashtag > Parens {{{2
syntax cluster typstHashtagParens
    \ contains=typstHashtagParen
            \ ,typstHashtagBrace
            \ ,typstHashtagBracket
            \ ,typstHashtagDollar
syntax region typstHashtagParen
    \ matchgroup=Noise start=/\v\#\(/ end=/\v\)/
    \ contains=@typstCode
syntax region typstHashtagBrace
    \ matchgroup=Noise start=/\v\#\{/ end=/\v\}/me=e-1
    \ contains=@typstCode
syntax region typstHashtagBracket
    \ matchgroup=Noise start=/\v\#\[/ end=/\v\]/
    \ contains=@typstMarkup
syntax region typstHashtagDollar
    \ matchgroup=Noise start=/\v\#\$/ end=/\v\$/
    \ contains=@typstMath


" Markup {{{1
syntax cluster typstMarkup
    \ contains=@typstCommon
            \ ,@typstHashtag
            \ ,@typstMarkupText
            \ ,@typstMarkupParens

" Markup > Text {{{2
syntax cluster typstMarkupText
    \ contains=typstMarkupRawInline
            \ ,typstMarkupRawBlock
            \ ,typstMarkupLabel
            \ ,typstMarkupReference
            \ ,typstMarkupHeading
            \ ,typstMarkupBulletList
            \ ,typstMarkupEnumList
            \ ,typstMarkupBold
            \ ,typstMarkupItalic
            \ ,typstMarkupUnderline
syntax match typstMarkupRawInline
    \ /`.*`/
syntax region typstMarkupRawBlock
    \ start=/```/ end=/```/
syntax match typstMarkupLabel
    \ /<\S\+>/
syntax match typstMarkupReference
    \ /@\S\+\s/
syntax match typstMarkupHeading
    \ /^=\{1,6}\s.*$/
syntax match typstMarkupBulletList
    \ /\v^\s*-\s+/
syntax match typstMarkupEnumList
    \ /\v^\s*(\+|\d+\.)\s+/
syntax region typstMarkupBold
    \ start=/\*/ skip=/\\\*/ end=/\*/ oneline
syntax region typstMarkupItalic
    \ start=/_/ skip=/\\_/ end=/_/ oneline
syntax region typstMarkupUnderline
    \ start=/#underline\[/ skip=/\\\]/ end=/\]/


" Markup > Parens {{{2
syntax cluster typstMarkupParens
    \ contains=typstMarkupDollar
syntax region typstMarkupDollar
    \ matchgroup=Number start=/\$/ end=/\$/
    \ contains=@typstMath


" Math {{{1
syntax cluster typstMath
    \ contains=@typstCommon
            \ ,@typstHashtag


" Highlighting {{{1

" Highlighting > Linked groups {{{2
highlight default link typstCommentBlock         Comment
highlight default link typstCommentLine          Comment
highlight default link typstCommentTodo          Todo
highlight default link typstCodeConditional      Conditional
highlight default link typstCodeRepeat           Repeat
highlight default link typstCodeKeyword          Keyword
highlight default link typstCodeConstant         Constant
highlight default link typstCodeNumberInteger    Number
highlight default link typstCodeNumberFloat      Number
highlight default link typstCodeNumberLength     Number
highlight default link typstCodeNumberAngle      Number
highlight default link typstCodeNumberRatio      Number
highlight default link typstCodeNumberFraction   Number
highlight default link typstCodeString           String
highlight default link typstCodeStatementWord    Statement
highlight default link typstCodeIdentifier       Identifier
highlight default link typstCodeFieldAccess      Identifier
highlight default link typstCodeFunction         Function
highlight default link typstCodeParen            Noise
highlight default link typstCodeBrace            Noise
highlight default link typstCodeBracket          Noise
highlight default link typstCodeDollar           Noise
highlight default link typstHashtagConditional   Conditional
highlight default link typstHashtagRepeat        Repeat
highlight default link typstHashtagKeyword       Keyword
highlight default link typstHashtagConstant      Constant
highlight default link typstHashtagStatementWord Statement
highlight default link typstHashtagIdentifier    Identifier
highlight default link typstHashtagFieldAccess   Identifier
highlight default link typstHashtagFunction      Function
highlight default link typstHashtagParen         Noise
highlight default link typstHashtagBrace         Noise
highlight default link typstHashtagBracket       Noise
highlight default link typstHashtagDollar        Noise
highlight default link typstMarkupRawInline      Macro
highlight default link typstMarkupRawBlock       Macro
highlight default link typstMarkupLabel          Structure
highlight default link typstMarkupReference      Structure
highlight default link typstMarkupHeading        Structure
highlight default link typstMarkupBulletList     Structure
highlight default link typstMarkupEnumList       Structure
highlight default link typstMarkupBold           Structure
highlight default link typstMarkupItalic         Structure
highlight default link typstMarkupUnderline      Structure
highlight default link typstMarkupDollar         Noise

" Highlighting > Typst Styling {{{2
highlight default typstMarkupBold           cterm=bold      gui=bold
highlight default typstMarkupItalic         cterm=italic    gui=italic
highlight default typstMarkupUnderline      cterm=underline gui=underline

" vim: foldlevel=0 tabstop=8 shiftwidth=4 softtabstop=4 expandtab
